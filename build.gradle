import groovy.json.JsonOutput
import groovy.json.JsonSlurper

plugins {
	id 'fabric-loom' version "1.14-SNAPSHOT"
	id 'maven-publish'
}

def is1215 = project.hasProperty("mc_target") && (project.mc_target == "1.21.5" || project.mc_target == "1.21.4")
def is1216 = project.hasProperty("mc_target") && (project.mc_target == "1.21.6" || project.mc_target == "1.21.7")
def is12111 = project.hasProperty("mc_target") && project.mc_target == "1.21.11"

ext {
	if (is12111) {
		minecraft_version = "1.21.11"
		loader_version = "0.18.4"
		fabric_version = "0.141.1+1.21.11"
		target_version = "1.21.11"
	} else if (is1216) {
		minecraft_version = "1.21.6"
		loader_version = "0.16.14"
		fabric_version = "0.127.1+1.21.6"
		target_version = "1.21.6-1.21.7"
	} else if (is1215) {
		minecraft_version = "1.21.5"
		loader_version = "0.16.13" // why am i not updating this?
		fabric_version = "0.121.0+1.21.5"
		target_version = "1.21.4-1.21.5"
	}
}

version = project.mod_version
group   = project.maven_group

// version print
println "==> Building Normalizer ${version} for Minecraft ${minecraft_version} " +
		"with Mojang mappings " +
		"and Fabric Loader ${loader_version}"


base {
	archivesName = "${project.archives_base_name}-${target_version}"
}

repositories {
	maven { url = "https://maven.shedaniel.me/" }
	maven { url = "https://maven.terraformersmc.com/releases/" }
}

dependencies {
	minecraft     "com.mojang:minecraft:${minecraft_version}"
	mappings       loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

	modApi("me.shedaniel.cloth:cloth-config-fabric:17.0.144") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	modApi "com.terraformersmc:modmenu:15.0.1"
}

processResources {
	inputs.property "version", project.version
	inputs.property "is12111", is12111
	inputs.property "is1216", is1216
	inputs.property "is1215", is1215

	filesMatching("fabric.mod.json") {
		expand(
				"version": inputs.properties.version,
				"mixin_type": (is12111 ? "mc12111" : "mc1214")
		)
	}
}

tasks.register('preprocessJava', Copy) {
	doFirst {
		delete "$buildDir/preprocessedSrc"
	}

	from 'src/main/java'
	into "$buildDir/preprocessedSrc"

	if (is12111) {
		exclude '**/mixin/mc1214/**'
	} else {
		exclude '**/mixin/mc12111/**'
	}

	include '**/*.java'

	filter { String line ->
		if (is1215 || is1216) {
			line = line.replace(
					'net.minecraft.util.Util',
					'net.minecraft.Util'
			)
		}

		if (is1215) {
			line = line.replace(
					'getCurrentVersion().name()',
					'getCurrentVersion().getName()',
			)
		}

		return line
	}

	doLast {
		fileTree("$buildDir/preprocessedSrc").matching {
			include '**/*.java'
		}
	}
}

compileJava {
	dependsOn preprocessJava
	source = fileTree("$buildDir/preprocessedSrc") { include '**/*.java' }

	doLast {
		delete "$buildDir/preprocessedSrc"
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}" }
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}